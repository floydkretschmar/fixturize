plugins {
    id "java"
    id "io.freefair.lombok" version "8.6"
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    sourceCompatibility = JavaVersion.VERSION_17
}

assert JavaVersion.VERSION_17.isCompatibleWith(JavaVersion.current());

ext {
    graalVMjsVersion = "23.0.5"
    guavaVersion = "33.2.1-jre"
    autoServiceVersion = "1.1.1"

    junitVersion = "5.10.0"
    assertJVersion = "3.25.1"
    mockitoVersion = "5.12.0"
    compileTestingVersion = "0.21.0"
    lombokVersion = "1.18.34"
}

dependencies {
    implementation "com.google.auto.service:auto-service:$autoServiceVersion"
    annotationProcessor "com.google.auto.service:auto-service:$autoServiceVersion"

    implementation "com.google.guava:guava:$guavaVersion"
    implementation "org.graalvm.js:js:$graalVMjsVersion"
    implementation "org.graalvm.js:js-scriptengine:$graalVMjsVersion"


    testImplementation platform("org.junit:junit-bom:$junitVersion")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:$assertJVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testImplementation "com.google.testing.compile:compile-testing:$compileTestingVersion"
    testImplementation "org.projectlombok:lombok:$lombokVersion"
}

test {
    useJUnitPlatform()
}

tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

tasks.register('javadocJar', Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'fixturize-core'
            version = project.version

            artifact sourcesJar
            artifact javadocJar

            pom {
                name.set("Fixturize")
                description.set("Library that uses java annotation processing to automatically generate fixtures for domain classes.")
                url.set("https://github.com/floydkretschmar/fixturize")
                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("https://github.com/floydkretschmar/fixturize/blob/main/LICENSE.txt")
                    }
                }
                developers {
                    developer {
                        id.set("floydkretschmar")
                        name.set("Floyd Kretschmar")
                        email.set("floyd.kretschmar@gmail.com")
                    }
                }
                scm {
                    connection.set("scm:git:git://github.com/floydkretschmar/fixturize.git")
                    developerConnection.set("scm:git:ssh://github.com/floydkretschmar/fixturize.git")
                    url.set("https://github.com/floydkretschmar/fixturize")
                }
            }
        }
    }
    repositories {
        maven {
            name = "MavenCentral"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            System.getenv("SIGNING_KEY_ID"),
            System.getenv("SIGNING_SECRET_KEY"),
            System.getenv("SIGNING_PASSWORD")
    )
    sign publishing.publications.mavenJava
}
