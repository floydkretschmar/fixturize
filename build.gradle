plugins {
    id "java"
    id 'pl.allegro.tech.build.axion-release' version '1.18.2'
}

scmVersion {
    repository {
        type = 'git'
    }
    tag {
        prefix = ''
    }
    versionIncrementer 'incrementPatch'
}

allprojects {
    group = "de.floydkretschmar"
    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }
}

configure(subprojects.findAll {
    it.name == 'fixturize-core'
}) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                groupId = project.group
                artifactId = project.name
                version = rootProject.version // Use the version set by the root project
            }
        }
        repositories {
            maven {
                name = "MavenCentral"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv("OSSRH_USERNAME")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                System.getenv("SIGNING_KEY_ID"),
                System.getenv("SIGNING_SECRET_KEY_RING_FILE"),
                System.getenv("SIGNING_PASSWORD")
        )
        sign publishing.publications.mavenJava
    }
}